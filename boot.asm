%include "bios.h"
%include "fat.h"

	[BITS 16]
	[ORG BOOT_ENTRY + 64]

	JMP 0:CORRECTION
CORRECTION:
	MOV AX, CS
	MOV ES, AX
	MOV SS, AX
	MOV DS, AX
	MOV SP, BOOT_ENTRY
	MOV BYTE [DRIVE_NUM], DL

	MOV SI, ENTRYSTR
	CALL PRINT
	
	XOR AX, AX
	MOV AL, BYTE [NUM_FATS]
	MUL WORD [SECTORS_PER_FAT]
	MOV WORD [CLUSTER_START], AX
	MOV AX, WORD [NUM_ENTRIES]
	MOV BX, DIR_SIZE
	MUL BX
	DIV WORD [BYTES_PER_SECTOR]
	ADD AX, WORD [NUM_HIDDEN]
	ADD WORD [CLUSTER_START], AX
	
	;Read the FAT into memory
	MOV BX, 0x7E00
	MOV CX, WORD [SECTORS_PER_FAT]
	MOV AX, WORD [NUM_HIDDEN]
	INC AX
LOADFAT:
	CALL READSECTOR
	ADD BX, 512
	INC AX
	LOOP LOADFAT

	;Get the root directory LBA
	MOV WORD [DIRSTART], BX
	PUSH AX
	XOR AX, AX
	MOV AL, BYTE [NUM_FATS]
	DEC AX
	MUL WORD [SECTORS_PER_FAT]
	POP DX
	ADD AX, DX
	DEC AX

	;Get the root directory size in sectors
	PUSH AX
	MOV AX, WORD [NUM_ENTRIES]
	MOV CX, 32
	MUL CX
	DIV WORD [BYTES_PER_SECTOR]
	MOV CX, AX
	POP AX
	
	;Load the root directory off disk
LOADROOT:
	CALL READSECTOR
	INC AX
	ADD BX, 512
	LOOP LOADROOT
	
	;Print out the files found (That are > 0 bytes)
	MOV AX, WORD [DIRSTART]
	MOV CX, WORD [NUM_ENTRIES]
LOOPDIR:
	MOV SI, AX
	MOV BL, BYTE [SI + ATTRIBUTES]
	CMP BL, EXTENDED_DIR
	JE SKIP_PRINT
	MOV BX, WORD [SI + BYTE_SIZE]
	OR BX, BX
	JE SKIP_PRINT
	CALL PRINT
SKIP_PRINT:
	ADD AX, DIR_SIZE
	LOOP LOOPDIR
DONEPRINT:
	CLI
	HLT

;Assumes AX=LBA, ES:BX=BUFFER
READSECTOR:
	PUSHA
	CALL LBA2CHS
	MOV DL, BYTE [DRIVE_NUM]
	MOV AL, 1
	MOV CL, BYTE [SECTOR]
	MOV CH, BYTE [TRACK]
	MOV AH, READSECTOR_B
	MOV DH, BYTE [HEAD]
	INT DISK_SERVICE
	JNC READSUCCESS
	MOV SI, BADREADSTR	;Bad sector read, Hang the machine
	CALL PRINT
	CLI
	HLT
READSUCCESS:
	POPA
	RET

CLUSTER2LBA:
	DEC AX
	DEC AX
	MUL WORD [SECTORS_PER_CLUSTER]
	ADD AX, WORD [CLUSTER_START]
	RET

LBA2CHS:
	PUSHA
	XOR DX, DX
	DIV WORD [SECTORS_PER_TRACK]
	INC DL
	;Save sector referenced by the LBA
	MOV BYTE [SECTOR], DL
	XOR DX, DX
	DIV WORD [NUM_HEADS]
	;Save head from LBA
	MOV BYTE [HEAD], DL
	;Save track from LBA
	MOV BYTE [TRACK], AL
	POPA
	RET

PRINT:
	PUSHA
	MOV AH, PUTCHAR
PRINTLOOP:
	LODSB
	OR AL, AL
	JZ ENDPRINT
	INT TEXT_SERVICE
	JMP PRINTLOOP
ENDPRINT:
	MOV AL, 13
	INT TEXT_SERVICE
	MOV AL, 10
	INT TEXT_SERVICE
	POPA
	RET

ENTRYSTR:	DB "Dosile boot started.", 0
BADREADSTR:	DB "Fatal read error. Halting.", 0
TRACK:		DB 0
HEAD:		DB 0
SECTOR:		DB 0
CLUSTER_START:	DW 0
DIRSTART:	DW 0

	TIMES 510 - 64 - ($ - $$) DB 0
	DW BOOT_SIGNATURE
